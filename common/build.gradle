apply plugin: 'com.android.library'
apply from: "${project.rootDir}/config.gradle"

android {

    compileSdkVersion project.ext.android.compileSdkVersion
    buildToolsVersion project.ext.android.buildToolsVersion

    defaultConfig {
        minSdkVersion project.ext.android.defaultConfig.minSdkVersion
        targetSdkVersion project.ext.android.defaultConfig.targetSdkVersion
        versionCode project.ext.android.defaultConfig.versionCode
        versionName project.ext.android.defaultConfig.versionName
    }

    lintOptions {
        checkReleaseBuilds project.ext.android.lintOptions.checkReleaseBuilds
        abortOnError project.ext.android.lintOptions.abortOnError
    }

    buildTypes {
        release {
            // 自定义BuildConfig参数
            buildConfigField "String", "SDK_VERSION", "\"1.0\""
            zipAlignEnabled true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            // 自定义BuildConfig参数
            buildConfigField "String", "SDK_VERSION", "\"1.0\""
            zipAlignEnabled false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }



    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project.ext.dependencies["appcompat-v7"]
    compile project.ext.dependencies["support-v4"]
    compile project.ext.dependencies["design"]
    compile project.ext.dependencies["okhttp3"]

    // fresco
    compile "com.facebook.fresco:imagepipeline-okhttp3:0.12.0"
    compile('com.facebook.fresco:fresco:0.12.0') {
        exclude group: 'com.nineoldandroids'
    }
}

task makeJar(type: Copy) {
    delete 'build/libs/damocles_common.jar'
    from('build/intermediates/bundles/release/')
    into('build/libs/')
    include('classes.jar')
    rename('classes.jar', 'damocles_common.jar')
}

makeJar.dependsOn(build)
